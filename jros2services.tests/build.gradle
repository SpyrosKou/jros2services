plugins {
  id 'com.diffplug.spotless'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  testImplementation project(':jros2services')
  testImplementation "io.github.lambdaprime:id.opentelemetry-exporters-pack-junit:3.0-SNAPSHOT"
  
  def junitVersion = "5.8.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}

sourceSets {
  test {
    java {
      srcDirs += ["src/gen/java"]
    }
  }
}

test {
  systemProperty "metrics.elastic.url", System.env.ELASTIC_URL + "/jros2client"
  useJUnitPlatform()
  testLogging {
    events "started", "failed"
  }
}

// run spotless and format code before the build
testClasses.dependsOn spotlessApply
